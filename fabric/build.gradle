plugins {
    // The shadow gradle plugin assists with repackaging a 3rd-party library jar within your own jar
    // In addition to ensuring that your end-users have the library available when they use your own thing,
    // it also helps avoid collisions with other things that are also using the same library.
    // ref: https://gist.github.com/SizableShrimp/949e7c219bfc94487a45226b64ac7749
    //
    // Latest Test Compatibility: gradle version 8.x shadow version 8.0.0+
    // ref: https://github.com/johnrengelman/shadow
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    // Loom sets the log4j config while in development (in prod the log4j config might be dictated by the launcher or just the game itself).
    // This loom gradle config specifies that loom uses our customized log4j config file.
    // ref: https://fabricmc.net/wiki/documentation:fabric_loom
    // (although we're using the architectury fork of fabric loom, this config remains unchanged)
    log4jConfigs.from(file("../common/src/main/resources/log4j.xml"))
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        url 'https://maven.terraformersmc.com/'
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    // Use this part of fabric api to generate asserts to resource packs,
    // so the lang resources can be loaded.
    // Use jij Jar-in-Jar style to dependent on this part,
    // so we needn't to depend on whole fabric-api.
    // ref: https://fabricmc.net/wiki/documentation:fabric_loom#dependency_configurations
    modApi include("net.fabricmc.fabric-api:fabric-resource-loader-v0:${rootProject.fabric_resource_loader_version}")
    modApi include("net.fabricmc.fabric-api:fabric-key-binding-api-v1:${rootProject.fabric_key_binding_api_version}")
    modApi include('me.shedaniel.cloth:cloth-config-fabric:15.0.140') {
        exclude group: 'net.fabricmc.fabric-api'
    }
    modImplementation 'com.terraformersmc:modmenu:11.0.2'

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand Map.of("version", project.version,
                "fabric_loader_version", rootProject.fabric_loader_version,
                "minecraft_version", rootProject.minecraft_version)
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    // "classifier" is removed in gradle 8, use "archiveClassifier" instead
    // ref: https://github.com/architectury/architectury-api/blob/18ba59c9041d3fa535278097ee4b3d574dfec6d2/fabric/build.gradle#L48
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
